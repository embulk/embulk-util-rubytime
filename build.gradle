plugins {
    id "java"
    id "maven-publish"
    id "signing"
    id "checkstyle"
}

repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.4.0-SNAPSHOT"
description "Almost Ruby-compatible date-time processor for Embulk and Embulk plugins"

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()

    crubyTestImplementation {
        extendsFrom testImplementation
    }
    crubyTestRuntimeOnly {
        extendsFrom testRuntimeOnly
    }
    jruby9_1_15TestImplementation {
        extendsFrom testImplementation
    }
    jruby9_1_15TestRuntimeOnly {
        extendsFrom testRuntimeOnly
    }
    jruby9_1_17TestImplementation {
        extendsFrom testImplementation
    }
    jruby9_1_17TestRuntimeOnly {
        extendsFrom testRuntimeOnly
    }
    jruby9_2_0TestImplementation {
        extendsFrom testImplementation
    }
    jruby9_2_0TestRuntimeOnly {
        extendsFrom testRuntimeOnly
    }
    jruby9_2_9TestImplementation {
        extendsFrom testImplementation
    }
    jruby9_2_9TestRuntimeOnly {
        extendsFrom testRuntimeOnly
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = "UTF-8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    crubyTest {
        java {
            srcDir "src/crubyTest/java"
        }
        compileClasspath += main.output
        runtimeClasspath += main.output
    }

    jruby9_1_15Test {
        java {
            srcDir "src/jrubyTest/java"
        }
        resources {
            srcDir "src/jrubyTest/resources"
        }
        compileClasspath += main.output
        runtimeClasspath += main.output
    }

    jruby9_1_17Test {
        java {
            srcDir "src/jrubyTest/java"
        }
        resources {
            srcDir "src/jrubyTest/resources"
        }
        compileClasspath += main.output
        runtimeClasspath += main.output
    }

    jruby9_2_0Test {
        java {
            srcDir "src/jrubyTest/java"
        }
        resources {
            srcDir "src/jrubyTest/resources"
        }
        compileClasspath += main.output
        runtimeClasspath += main.output
    }

    jruby9_2_9Test {
        java {
            srcDir "src/jrubyTest/java"
        }
        resources {
            srcDir "src/jrubyTest/resources"
        }
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

dependencies {
    testImplementation platform(libs.junit5.bom)
    testImplementation libs.bundles.junit5.implementation

    testRuntimeOnly libs.bundles.junit5.runtime

    jruby9_1_15TestImplementation "org.jruby:jruby-complete:9.1.15.0"
    jruby9_1_17TestImplementation "org.jruby:jruby-complete:9.1.17.0"
    jruby9_2_0TestImplementation "org.jruby:jruby-complete:9.2.0.0"
    jruby9_2_9TestImplementation "org.jruby:jruby-complete:9.2.9.0"
}

javadoc {
    title = "${project.name} v${project.version}"
    options {
        locale = "en_US"
        encoding = "UTF-8"
        overview = "src/main/html/overview.html"
        links "https://docs.oracle.com/javase/8/docs/api/"
    }
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
        // Not including NOTICE because it has license notice only about test dependencies.
    }
}

sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
        // Not including NOTICE because it has license notice only about test dependencies.
    }
}

javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
        // Not including NOTICE because it has license notice only about test dependencies.
    }
}

// A safer and strict alternative to: "dependencies" (and "dependencies --write-locks")
//
// This task fails explicitly when the specified dependency is not available.
// In contrast, "dependencies (--write-locks)" does not fail even when a part the dependencies are unavailable.
//
// https://docs.gradle.org/8.7/userguide/dependency_locking.html#generating_and_updating_dependency_locks
task checkDependencies {
    notCompatibleWithConfigurationCache("The task \"checkDependencies\" filters configurations at execution time.")
    doLast {
        configurations.findAll { it.canBeResolved }.each { it.resolve() }
    }
}

// It should not publish a `.module` file in Maven Central.
// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://www.embulk.org/"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        name = "Dai MIKURUBE"
                        email = "dmikurube@treasure-data.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-util-rubytime.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-util-rubytime.git"
                    url = "https://github.com/embulk/embulk-util-rubytime"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
        logger.lifecycle("Signing with an in-memory key.")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.maven
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

task crubyTest(type: Test, description: "Runs tests with CRuby.", group: "Verification") {
    classpath = sourceSets.crubyTest.runtimeClasspath
    testClassesDirs = sourceSets.crubyTest.output.classesDirs

    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

task jruby9_1_15Test(type: Test, description: "Runs tests with JRuby 9.1.15.", group: "Verification") {
    classpath = sourceSets.jruby9_1_15Test.runtimeClasspath
    testClassesDirs = sourceSets.jruby9_1_15Test.output.classesDirs

    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

task jruby9_1_17Test(type: Test, description: "Runs tests with JRuby 9.1.17.", group: "Verification") {
    classpath = sourceSets.jruby9_1_17Test.runtimeClasspath
    testClassesDirs = sourceSets.jruby9_1_17Test.output.classesDirs

    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

task jruby9_2_0Test(type: Test, description: "Runs tests with JRuby 9.2.0.", group: "Verification") {
    classpath = sourceSets.jruby9_2_0Test.runtimeClasspath
    testClassesDirs = sourceSets.jruby9_2_0Test.output.classesDirs

    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

task jruby9_2_9Test(type: Test, description: "Runs tests with JRuby 9.2.9.", group: "Verification") {
    classpath = sourceSets.jruby9_2_9Test.runtimeClasspath
    testClassesDirs = sourceSets.jruby9_2_9Test.output.classesDirs

    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}
